// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
    static Array twoToThe;
    static int twoqy;

    /** Initializes the library. */
    function void init() {
        var int i, power;
        let twoToThe = Array.new(16);
        let i = 0;
        let power = 1;
        while (i < 16) {
            let twoToThe[i] = power;
            let power = power + power;
            let i = i + 1;
        }
        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x > 0) {
            return x;
        } else {
            return -x;
        }
    }

    /** Returns the product of x and y.
     *  When a Jack compiler detects the multiplication operator '*' in the
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int i, sum, shiftedX;
        let i = 0;
        let sum = 0;
        let shiftedX = x;
        while (i < 16) {
            if ( ~(Math.bit(y, i) = 0) ) {
                let sum = sum + shiftedX;
            }
            let shiftedX = shiftedX + shiftedX;
            let i = i + 1;
        }

        return sum;
    }

    function int bit(int x, int i) {
        return twoToThe[i] & x;
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        var int a, b, div;
        var boolean plus;
        let a = Math.abs(x);
        let b = Math.abs(y);

        let plus = true;
        if ((x < 0) & (y < 0)) {
            let plus = true;
        }
        if ((x > 0) & (y < 0)) {
            let plus = false;
        }
        if ((x < 0) & (y > 0)) {
            let plus = false;
        }
        if ((x > 0) & (y > 0)) {
            let plus = true;
        }

        let div = Math._divide(a, b);
        if (plus) {
            return div;
        } else {
            return -div;
        }
    }

    function int _divide(int x, int y) {
        var int q;
        if ((y > x) | (y < 0)){
            let twoqy = 0;
            return 0;
        }
        let q = Math._divide(x, (y+y));
        if ((x - twoqy) < y) {
            return q + q;
        } else {
            let twoqy = twoqy + y;
            return q + q + 1;
        }
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y, i, sq;
        let y = 0;
        let i = 8;
        while (i > -1) {
            let sq = (y + twoToThe[i]) * (y + twoToThe[i]);
            if (((sq < x) | (sq = x)) & (sq > 0)) {
                let y = y + twoToThe[i];
            }
            let i = i - 1;
        }
        return y;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if (a < b) {
            return b;
        } else {
            return a;
        }
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if (a < b) {
            return a;
        } else {
            return b;
        }
    }
}
