// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    static int key;

    /** Initializes the keyboard. */
    function void init() {
        let key = 0;
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(24576);
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char c;
        do Output.printChar(0); // cursor
        while (Keyboard.keyPressed() = 0) {}
        let c = Keyboard.keyPressed();
        while (~(Keyboard.keyPressed() = c)) {}
        do Output.printChar(129);
        do Output.printChar(c);
        return c;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String str;
        var char c;
        do Output.printString(message);
        let str = String.new(100);
        while (true) {
            let c = Keyboard.readChar();
            while (Keyboard.keyPressed() = c) {}
            if (c = 128) { // newline
                return str;
            }
            if (c = 129) { // backspace
                if (str.length() = 0) {
                    do Output.printString("ERR");
                    do Sys.halt();
                }
                do str.eraseLastChar();
            } else {
                let str = str.appendChar(c);
            }
        }
        return str;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var char c, begin;
        var String str;
        do Output.printString(message);
        let str = String.new(7);
        let begin = 1;
        while (true) {
            let c = Keyboard.readChar();
            while (Keyboard.keyPressed() = c) {}
            if (c = 128) { // newline
                return str.intValue();
            }
            if (c = 129) { // backspace
                if (str.length() = 0) {
                    do Sys.halt();
                }
                if (str.length() = 1) {
                    let begin = 1;
                }
                do str.eraseLastChar();
            }
            if ((c > 47) & (c < 58)) { // 0-9
                let str = str.appendChar(c);
            }
            if ((c = 45) & (begin = 1)) { // minus
                let str = str.appendChar(c);
                let begin = 0;
            }
        }
        return 0;
    }
}
